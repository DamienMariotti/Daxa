#version 450
#extension GL_KHR_vulkan_glsl : enable

layout(local_size_x = 8, local_size_y = 8, local_size_z = 8) in;

#include <chunkgen/common.glsl>
#include <chunkgen/world/noise.glsl>

uint gen_block(in vec3 b_pos) {
    float val = terrain_noise(b_pos);
    float b_val = biome_noise(b_pos);
    float u_val = underworld_noise(b_pos);
    float r = rand(b_pos);

    float water_level = WATER_LEVEL;

    uint biome_id = BiomeID_Plains;
    uint block_id = BlockID_Air;

    if (b_val < 0.07)
        biome_id = BiomeID_Forest;
    else if (b_val > 0.22)
        biome_id = BiomeID_Desert;
    if (b_pos.y - water_level > -4 + r * 3 && b_pos.y - water_level < 4 + r * 3 && val < 0.05 + r * 0.1 && val > -0.05 - r * 0.1)
        biome_id = BiomeID_Beach;

    if (val > 0.1f) {
        if (b_pos.y > water_level + 150 + val * 20 + r * 60) {
            biome_id = BiomeID_Underworld;
            if (u_val > 0) {
                if (b_pos.y > 760 + r * 6) {
                    block_id = BlockID_Bedrock;
                } else {
                    block_id = BlockID_CompressedStone;
                }
            } else if (b_pos.y > 650) {
                block_id = BlockID_Lava;
            }
        } else {
            block_id = BlockID_Stone;
        }
    } else {
        if (b_pos.y > water_level) {
            block_id = BlockID_Water;
        }
    }

    biome_id = (biome_id << 16) & BIOME_ID_MASK;
    return block_id | biome_id;
}

void main() {
    uvec3 global_i = gl_GlobalInvocationID.xyz;
    vec3 block_pos = vec3(global_i) + p.pos.xyz;
    imageStore(output_image, ivec3(global_i), uvec4(gen_block(block_pos)));
}
